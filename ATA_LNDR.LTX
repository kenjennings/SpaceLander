10 REM SPACE LANDER GAME (C64 BASIC) ATARI BASIC PORT
11 REM ORIGINAL C64 VERSION BY CHRIS GARRETT
12 REM IN COMPUTE'S GAZETTE VOLUME 1 ISSUE 5
13 REM ATARI BASIC PORT BY KEN JENNINGS, NOVEMBER 2025
15 DIM SCR(24):REM OPTIMIZING SCREEN REFERENCES
25 CH=764:REM READING KEYBOARD CODES
30 GOSUB 18010:REM ONE TIME INITIALIZATION
35 GOSUB 17010:REM TITLE SCREEN AND DIFFICULTY
40 GOSUB 16010:REM INITIALIZE NEW GAME
200 REM GAME LOOP - STORE OLD VALUES - GET JOYSTICK
205 OX=X:OY=INT(Y):JSTICK=STICK(0)
215 IF JSTICK=7 AND X<39 THEN X=X+1
220 IF JSTICK=11 AND X>0 THEN X=X-1
225 REM THRUST
230 IF FUEL>0 AND JSTICK=14 AND THRUST<GRAVITYTWO THEN THRUST=THRUST+0.25
235 REM SPEED IS SPEED MINUS THRUST PLUS GRAVITY
240 REM BUT SPEED CAN NOT EXCEED 1
245 SPEED=SPEED-THRUST+GRAVITY:IF SPEED>1 THEN SPEED=1
246 IF SPEED<-1 THEN SPEED=-1
250 IF THRUST>GRAVITYTWO THEN THRUST=GRAVITYTWO
255 IF FUEL>0 AND THRUST>0 THEN FUEL=FUEL-1
257 IF FUEL<0 THEN FUEL=0
265 IF THRUST>0 THEN THRUST=THRUST-0.2
270 IF FUEL=0 OR THRUST<=0 THEN THRUST=0
275 REM HEIGHT IS PREVIOUS HEIGHT PLUS SPEED
280 Y=Y+SPEED:IF Y<1 THEN Y=1
285 POSITION 8,0:? FUEL;" "
290 POSITION 8,1:? FUEL;"   "
295 POSITION 8,2:? ABS(THRUST);"   "
300 REM DETERMINE SUCCESS
305 IF Y>=GROUND THEN GOTO 610
310 REM OTHERWISE REPEAT
315 GOSUB 510
320 GOTO 205
500 REM ERASE AND REDRAW THE LANDER 
510 IF OX<>X OR OY<>Y AND INT(Y)<=GROUND THEN POKE SCR(INT(OY))+OX,0:POKE SCR(INT(OY+1))+OX,0
515 REM DRAW THE SHIP
520 POKE SCR(INT(Y))+X,80:REM SUPPOSED TO BE CLUB?
525 IF JSTICK=14 AND Y<GROUND THEN POKE SCR(INT(Y)+1)+X,54
530 RETURN 
600 REM SUCCESS OR FAILURE TEST (TS IS TARGET SPEED)
610 IF SPEED<=TARGETSPEED THEN POSITION 15,17:? "TOUCHDOWN!":POKE SCR(INT(Y))+X,128
615 IF SPEED>TARGETSPEED THEN GOSUB 20010
10000 POSITION 14,10:? "PLAY AGAIN?"
10005 K=PEEK(CH):IF K=255 THEN GOTO 10005
10010 POKE CH,255
10015 IF K=35 THEN GRAPHICS 0:END 
10020 IF K=43 THEN GOTO 35
10025 GOTO 10005
15000 REM DRAW GROUND AND STATS LABELS
15010 FOR I=0 TO 39:POKE SCR(GROUND)+I,128:NEXT I:REM DRAW GROUND
15015 POSITION 0,0
15020 ? "  FUEL":? " SPEED":? "THRUST"
15025 RETURN 
16000 REM NEW GAME INITIALIZATION
16010 ? CHR$(125)
16015 GROUND=20:REM GROUND IS ROW 20
16020 GOSUB 15010
16025 X=20:Y=1:SPEED=0.5:THRUST=0:FUEL=30:GRAVITY=0.15:GRAVITYTWO=GRAVITY*2
16030 RETURN 
17000 REM TITLE SCREEN AND DIFFICULTY INPUT
17010 ? CHR$(125)
17015 POSITION 13,0:? "SPACE LANDER"
17020 POSITION 5,2:? "Use Joystick..."
17025 POSITION 5,3:? "UP=THRUST, Steer LEFT and RIGHT"
17030 POSITION 10,5:? "Choose Difficulty:"
17035 POSITION 14,7:? "1: EASY"
17040 POSITION 14,9:? "2: MEDIUM"
17045 POSITION 14,11:? "3: HARD"
17050 TARGETSPEED=0
17055 K=PEEK(CH):IF K=255 THEN GOTO 17055
17060 POKE CH,255
17065 REM TARGET SPEED (SETS DIFFICULTY)
17070 IF K=31 THEN TARGETSPEED=0.55
17075 IF K=30 THEN TARGETSPEED=0.5
17080 IF K=26 THEN TARGETSPEED=0.4
17085 IF TARGETSPEED=0 THEN GOTO 17055
17090 RETURN 
18000 REM ONE TIME SETUP - SETUP SCR() ARRAY
18010 GRAPHICS 0:SETCOLOR 2,0,0:REM CLEAR SCREEN, BLACK COLOR
18015 POKE 82,0:POKE 752,1:REM LEFT BORDER 0, TURN OFF CURSOR
18020 ? CHR$(125);
18025 SCREENMEM=PEEK(88)+PEEK(89)*256
18030 FOR I=0 TO 23:SCR(I)=SCREENMEM+40*I:NEXT I
18035 RETURN 
20000 REM CRASH
20010 POSITION 15,17:? "OOF! SLOW DOWN!"
20015 POKE SCR(INT(Y))+X,10:POKE SCR(INT(Y-1))+X,10
20020 POKE SCR(INT(Y))+X+1,10:POKE SCR(INT(Y))+X-1,10
20025 RETURN 
